dnl Process this file with autoconf to produce a configure script

AC_INIT(epylog.spec)
PACKAGE_TARNAME=epylog
PACKAGE_VERSION=1.0.6

dnl Package information.
PACKAGE=$PACKAGE_TARNAME
VERSION=$PACKAGE_VERSION
AC_SUBST(VERSION)
AC_SUBST(PACKAGE)

dnl Default prefix.
AC_PREFIX_DEFAULT(/usr)

dnl top_builddir.
top_builddir=`pwd`
AC_SUBST(top_builddir)

AC_SUBST(docdir)
AC_SUBST(mandir)
AC_SUBST(sysconfdir)

dnl Checks for programs.
AC_PROG_INSTALL

AC_MSG_CHECKING(for --with-python)
AC_ARG_WITH(python, [  --with-python=BIN       Path to specific Python binary],
[
  AC_SUBST(PYTHON_BIN)
  PYTHON_BIN="$withval"
  AC_MSG_RESULT($PYTHON_BIN)
],
AC_MSG_RESULT(no))

# check for Python executable
if test -z "$PYTHON_BIN"; then
  AC_PATH_PROG(PYTHON_BIN, python)
  if test -z "$PYTHON_BIN"; then
    AC_MSG_ERROR(python binary not found in path)
  fi
fi

AC_SUBST(CRON_DIR)
CRON_DIR=$sysconfdir/cron.daily
AC_MSG_CHECKING(for --with-crondir)
AC_ARG_WITH(crondir, [  --with-crondir=DIR      Where to install the cron script],
[
  CRON_DIR="$withval"
  AC_MSG_RESULT($CRON_DIR)
],
AC_MSG_RESULT(will use $CRON_DIR))

# find out python version
AC_MSG_CHECKING(Python version)
PyVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[:3]'`]
PyMAJVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[:1]'`]
PyMINVERSION=`$PYTHON_BIN -c ['import sys; print sys.version[2:3]'`]
AC_MSG_RESULT($PyVERSION)

# make sure Python is version 2
if test "$PyMAJVERSION" -lt "2" || \
   (test "$PyMAJVERSION" -eq "2" && test "$PyMINVERSION" -lt "2"); then
  AC_MSG_ERROR([This version of epylog requires at least Python version 2.2.  
The one you have seems to be $PyVERSION.  You can specify an alternate python 
by using (for example) --with-python=/usr/bin/python2.2])
fi

# check for libxml2
AC_MSG_CHECKING(for libxml2)
if ! $PYTHON_BIN -c 'import libxml2' 2>/dev/null; then
   AC_MSG_ERROR([libxml2 not found.
This version of epylog requires libxml2-python. 
See http://xmlsoft.org/ or install libxml2-python rpm.])
fi
AC_MSG_RESULT(yes)

# find out compiled in install prefix
AC_MSG_CHECKING(Python install prefix)
PyEXEC_INSTALLDIR=`$PYTHON_BIN -c "import sys; print sys.exec_prefix"`
AC_MSG_RESULT($PyEXEC_INSTALLDIR)


# this is where the Python libraries will get installed if 
# --with-python-dirs is set
AC_SUBST(PY_STD_LIB)
PY_STD_LIB=${PyEXEC_INSTALLDIR}/lib/python${PyVERSION}

# otherwise, use this (relative to $libdir)
AC_SUBST(PY_PREFIX_LIB)
PY_PREFIX_LIB=$libdir/python${PyVERSION}

# now figure out which of these to use.  On most systems with normal defaults,
# these will be the same anyway.
AC_SUBST(PY_MODULE_DIR)
AC_MSG_CHECKING(for --with-python-dirs)
AC_ARG_WITH(python-dirs, [  --with-python-dirs      Put modules in python's site-packages dir],
[
  PY_MODULE_DIR=$withval
  AC_MSG_RESULT(yes)
],
[
  PY_MODULE_DIR=$PY_PREFIX_LIB/site-packages
  AC_MSG_RESULT(will use $PY_MODULE_DIR)
])

AC_SUBST(PERL_MODULE_DIR)
AC_MSG_CHECKING(for --with-site-perl)
AC_ARG_WITH(site-perl, [  --with-site-perl        Put perl module in site_perl dir],
[
  PERL_MODULE_DIR=$withval
  AC_MSG_RESULT(yes)
],
[
  #eval `perl '-V:installsitelib'`
  PERL_MODULE_DIR=$libdir/perl5/site_perl
  AC_MSG_RESULT(will use $PERL_MODULE_DIR)
])

AC_SUBST(TEMP_DIR)
AC_MSG_CHECKING(for --with-temp-dir)
AC_ARG_WITH(temp-dir, [  --with-temp-dir         Use this for temp dir (/var/tmp)],
[
  TEMP_DIR=$withval
  AC_MSG_RESULT(yes)
],
[
  TEMP_DIR=/var/tmp
  AC_MSG_RESULT(will use $TEMP_DIR)
])

AC_SUBST(LYNX_BIN)
AC_MSG_CHECKING(for --with-lynx)
AC_ARG_WITH(lynx, [  --with-lynx             Lynx/links/w3m location (/usr/bin/lynx)],
[
  LYNX_BIN=$withval
  AC_MSG_RESULT(yes)
],
[
  AC_MSG_RESULT(no)
  for lynxpath in /usr/bin/lynx /usr/bin/links /usr/bin/w3m; do
    AC_MSG_CHECKING(for $lynxpath)
    if ! test -x $lynxpath; then
      AC_MSG_RESULT(no)
    else
      AC_MSG_RESULT(yes)
      LYNX_BIN=$lynxpath
      break
    fi
  done
  if test -z "$LYNX_BIN"; then
    AC_MSG_ERROR(Lynx not found. Please specify with --with-lynx)
  fi
])

AC_OUTPUT_COMMANDS(chmod +x compiledir)
AC_OUTPUT(Makefile cron/Makefile etc/Makefile etc/modules.d/Makefile \
                   perl/Makefile man/Makefile modules/Makefile \
                   py/Makefile compiledir)
